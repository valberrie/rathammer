const std = @import("std");
const builtin = @import("builtin");
const IS_DEBUG = builtin.mode == .Debug;
const util = @import("util.zig");

const graph = @import("graph");
const Rec = graph.Rec;
const Vec2f = graph.Vec2f;
const V3f = graph.za.Vec3;
const vpk = @import("vpk.zig");
const edit = @import("editor.zig");
const Editor = @import("editor.zig").Context;
const Vec3 = V3f;
const Os9Gui = graph.gui_app.Os9Gui;
const Gui = graph.Gui;
const Split = @import("splitter.zig");
const editor_view = @import("editor_views.zig");
const G = graph.RGui;
const LaunchWindow = @import("windows/launch.zig").LaunchWindow;
const NagWindow = @import("windows/savenag.zig").NagWindow;
const PauseWindow = @import("windows/pause.zig").PauseWindow;
const ConsoleWindow = @import("windows/console.zig").Console;
const InspectorWindow = @import("windows/inspector.zig").InspectorWindow;
const Ctx2dView = @import("view_2d.zig").Ctx2dView;
const panereg = @import("pane.zig");
const json_map = @import("json_map.zig");

const Conf = @import("config.zig");

//Deprecate this please
//wrapper to make the old gui stuff work with pane reg
//singleton on kind
pub const OldGuiPane = struct {
    const Self = @This();
    const guis = graph.RGui;
    const Gui = guis.Gui;

    const Kind = enum {
        texture,
        model,
        model_view,
    };

    vt: panereg.iPane,

    editor: *Editor,
    os9gui: *Os9Gui,
    kind: Kind,

    pub fn create(alloc: std.mem.Allocator, ed: *Editor, kind: Kind, os9gui: *Os9Gui) !*panereg.iPane {
        var ret = try alloc.create(@This());
        ret.* = .{
            .vt = .{
                .deinit_fn = &deinit,
                .draw_fn = &draw_fn,
            },
            .kind = kind,
            .os9gui = os9gui,
            .editor = ed,
        };
        return &ret.vt;
    }

    pub fn draw_fn(vt: *panereg.iPane, pane_area: graph.Rect, editor: *Editor, vd: panereg.ViewDrawState, pane_id: panereg.PaneId) void {
        const self: *@This() = @alignCast(@fieldParentPtr("vt", vt));
        switch (self.kind) {
            .model => {
                editor.asset_browser.drawEditWindow(pane_area, self.os9gui, editor, .model) catch return;
            },
            .texture => {
                editor.asset_browser.drawEditWindow(pane_area, self.os9gui, editor, .texture) catch return;
            },
            .model_view => {
                _ = editor.panes.grab.trySetGrab(pane_id, editor.win.mouse.left == .high);
                editor.asset_browser.drawModelPreview(
                    editor.win,
                    pane_area,
                    vd.camstate,
                    editor,
                    vd.draw,
                ) catch return;
            },
        }
    }

    pub fn deinit(vt: *panereg.iPane, alloc: std.mem.Allocator) void {
        const self: *@This() = @alignCast(@fieldParentPtr("vt", vt));
        alloc.destroy(self);
    }
};

pub fn dpiDetect(win: *graph.SDL.Window) !f32 {
    const sc = graph.c.SDL_GetWindowDisplayScale(win.win);
    if (sc == 0)
        return error.sdl;
    return sc;
}

var font_ptr: ?*graph.OnlineFont = null;
fn flush_cb() void {
    if (font_ptr) |fp|
        fp.syncBitmapToGL();
}

pub fn pauseLoop(win: *graph.SDL.Window, draw: *graph.ImmediateDrawingContext, win_vt: *G.iWindow, gui: *G.Gui, gui_dstate: G.DrawState, loadctx: *edit.LoadCtx, editor: *Editor, should_exit: bool) !enum { cont, exit, unpause } {
    if (!editor.paused)
        return .unpause;
    if (win.isBindState(editor.config.keys.quit.b, .rising) or should_exit)
        return .exit;
    win.pumpEvents(.wait);
    win.grabMouse(false);
    try draw.begin(0x3d8891ff, win.screen_dimensions.toF());
    draw.real_screen_dimensions = win.screen_dimensions.toF();
    try editor.update(win);

    {
        const max_w = gui.style.config.default_item_h * 30;
        const area = graph.Rec(0, 0, draw.screen_dimensions.x, draw.screen_dimensions.y);
        const w = @min(max_w, area.w);
        const side_l = (area.w - w);
        const winrect = area.replace(side_l, null, w, null);
        const wins = &.{win_vt};
        try gui.pre_update(wins);
        try gui.updateWindowSize(win_vt, winrect);
        try gui.update(wins);
        try gui.draw(gui_dstate, false, wins);
        gui.drawFbos(draw, wins);
    }
    try draw.flush(null, null);
    try loadctx.loadedSplash(win.keys.len > 0);

    try draw.end(editor.draw_state.cam3d);
    win.swap();
    return .cont;
}

const log = std.log.scoped(.app);
pub fn wrappedMain(alloc: std.mem.Allocator, args: anytype) !void {
    var env = try std.process.getEnvMap(alloc);
    defer env.deinit();

    const app_cwd = blk: {
        switch (builtin.target.os.tag) {
            .linux => if (env.get("APPDIR")) |appdir| { //For appimage
                break :blk std.fs.cwd().openDir(appdir, .{}) catch {
                    log.err("Unable to open $APPDIR {s}", .{appdir});
                    break :blk std.fs.cwd();
                };
            },
            else => {},
        }
        break :blk std.fs.cwd();
    };
    //Relative to app_cwd
    const xdg_dir = (env.get("XDG_CONFIG_DIR"));

    const config_dir: std.fs.Dir = blk: {
        if (args.config != null)
            break :blk std.fs.cwd();

        var config_path = std.ArrayList(u8).init(alloc);
        defer config_path.deinit();
        if (xdg_dir) |x| {
            try config_path.writer().print("{s}/rathammer", .{x});
        } else {
            switch (builtin.target.os.tag) {
                .windows => break :blk app_cwd,
                else => {
                    if (env.get("HOME")) |home| {
                        try config_path.writer().print("{s}/.config/rathammer", .{home});
                    } else {
                        log.info("XDG_CONFIG_HOME and $HOME not defined, using config in app dir", .{});
                        break :blk app_cwd;
                    }
                },
            }
        }
        break :blk app_cwd.makeOpenPath(config_path.items, .{}) catch break :blk app_cwd;
    };
    // if user has specified a config, don't copy
    const copy_default_config = args.config == null;
    if (config_dir.openFile(args.config orelse "config.vdf", .{})) |f| {
        f.close();
    } else |_| {
        if (copy_default_config) {
            log.info("config.vdf not found in config dir, copying default", .{});
            try app_cwd.copyFile("config.vdf", config_dir, "config.vdf", .{});
        }
    }

    const load_timer = try std.time.Timer.start();
    var loaded_config = Conf.loadConfigFromFile(alloc, config_dir, "config.vdf") catch |err| {
        log.err("User config failed to load with error {!}", .{err});
        return error.failedConfig;
    };
    defer loaded_config.deinit();
    const config = loaded_config.config;
    var win = try graph.SDL.Window.createWindow("Rat Hammer", .{
        .window_size = .{ .x = config.window.width_px, .y = config.window.height_px },
        .frame_sync = .adaptive_vsync,
        .gl_major_version = 4,
        .gl_minor_version = 2,
    });
    defer win.destroyWindow();

    const Preset = struct {
        dpi: f32 = 1,
        fh: f32 = 25,
        ih: f32 = 14,
        scale: f32 = 2,

        pub fn distance(_: void, item: @This(), key: @This()) f32 {
            return @abs(item.dpi - key.dpi);
        }
    };

    const DPI_presets = [_]Preset{
        .{ .dpi = 1, .fh = 14, .ih = 25, .scale = 1 },
        .{ .dpi = 1.7, .fh = 24, .ih = 42 },
    };
    const sc = args.display_scale orelse try dpiDetect(&win);
    edit.log.info("Detected a display scale of {d}", .{sc});
    const dpi_preset = blk: {
        const default_scaled = Preset{ .fh = 20 * sc, .ih = 25 * sc, .scale = 2 };
        const max_dpi_diff = 0.3;
        const index = util.nearest(Preset, &DPI_presets, {}, Preset.distance, .{ .dpi = sc }) orelse break :blk default_scaled;
        const p = DPI_presets[index];
        if (@abs(p.dpi - sc) > max_dpi_diff)
            break :blk default_scaled;
        edit.log.info("Matching dpi preset number: {d}, display scale: {d}, font_height {d}, item_height {d},", .{ index, p.dpi, p.fh, p.ih });
        break :blk p;
    };

    const scaled_item_height = args.gui_item_height orelse @trunc(dpi_preset.ih);
    const scaled_text_height = args.gui_font_size orelse @trunc(dpi_preset.fh);
    const gui_scale = args.gui_scale orelse dpi_preset.scale;
    edit.log.info("gui Size: {d} text ", .{scaled_text_height});

    if (!graph.SDL.Window.glHasExtension("GL_EXT_texture_compression_s3tc")) return error.glMissingExt;
    var draw = graph.ImmediateDrawingContext.init(alloc);
    defer draw.deinit();
    var font = try graph.Font.init(alloc, app_cwd, args.fontfile orelse "ratasset/roboto.ttf", scaled_text_height, .{
        .codepoints_to_load = &(graph.Font.CharMaps.Default),
    });
    defer font.deinit();
    const splash = graph.Texture.initFromImgFile(alloc, app_cwd, "ratasset/small.png", .{}) catch edit.missingTexture();

    var loadctx = edit.LoadCtx{
        .draw = &draw,
        .font = &font,
        .win = &win,
        .splash = splash,
        .timer = try std.time.Timer.start(),
        .gtimer = load_timer,
        .expected_cb = 100,
    };

    var time_init = try std.time.Timer.start();

    var editor = try Editor.init(alloc, if (args.nthread) |nt| @intFromFloat(nt) else null, config, args, &win, &loadctx, &env, app_cwd, config_dir);
    defer editor.deinit();
    std.debug.print("edit init took {d} us\n", .{time_init.read() / std.time.ns_per_us});

    var os9gui = try Os9Gui.init(alloc, try app_cwd.openDir("ratgraph", .{}), gui_scale, .{
        .cache_dir = editor.dirs.pref,
        .font_size_px = scaled_text_height,
        .item_height = scaled_item_height,
        .font = &font.font,
    });
    defer os9gui.deinit();
    draw.preflush_cb = &flush_cb;
    font_ptr = os9gui.ofont;

    loadctx.cb("Loading gui");
    var gui = try G.Gui.init(alloc, &win, editor.dirs.pref, try app_cwd.openDir("ratgraph", .{}), &font.font);
    defer gui.deinit();
    gui.style.config.default_item_h = scaled_item_height;
    gui.style.config.text_h = scaled_text_height;
    gui.scale = gui_scale;
    gui.tint = config.gui_tint;
    const gui_dstate = G.DrawState{
        .ctx = &draw,
        .font = &font.font,
        .style = &gui.style,
        .gui = &gui,
        .scale = gui_scale,
    };
    const inspector_win = InspectorWindow.create(&gui, editor);
    const pause_win = try PauseWindow.create(&gui, editor, app_cwd);
    try gui.addWindow(&pause_win.vt, Rec(0, 300, 1000, 1000));
    try gui.addWindow(&inspector_win.vt, Rec(0, 300, 1000, 1000));
    const nag_win = try NagWindow.create(&gui, editor);
    try gui.addWindow(&nag_win.vt, Rec(0, 300, 1000, 1000));

    const launch_win = try LaunchWindow.create(&gui, editor);
    if (args.map == null) { //Only build the recents list if we don't have a map
        var timer = try std.time.Timer.start();
        if (config_dir.openFile("recent_maps.txt", .{})) |recent| {
            const slice = try recent.reader().readAllAlloc(alloc, std.math.maxInt(usize));
            defer alloc.free(slice);
            var it = std.mem.tokenizeScalar(u8, slice, '\n');
            while (it.next()) |filename| {
                const EXT = ".ratmap";
                if (std.mem.endsWith(u8, filename, EXT)) {
                    if (std.fs.cwd().openFile(filename, .{})) |recent_map| {
                        const qoi_data = json_map.getFileFromTar(alloc, recent_map, "thumbnail.qoi") catch continue;

                        defer alloc.free(qoi_data);
                        const qoi = graph.Bitmap.initFromQoiBuffer(alloc, qoi_data) catch continue;
                        const rec = LaunchWindow.Recent{
                            .name = try alloc.dupe(u8, filename[0 .. filename.len - EXT.len]),
                            .tex = graph.Texture.initFromBitmap(qoi, .{}),
                        };
                        qoi.deinit();

                        recent_map.close();
                        try launch_win.recents.append(rec);
                    } else |_| {}
                }
            }
        } else |_| {}

        std.debug.print("Recent build in {d} ms\n", .{timer.read() / std.time.ns_per_ms});
    }
    try gui.addWindow(&launch_win.vt, Rec(0, 300, 1000, 1000));

    var console_active = false;
    const console_win = try ConsoleWindow.create(&gui, editor, &editor.shell.cb_vt);
    try gui.addWindow(&console_win.vt, Rec(0, 0, 800, 600));

    const main_3d_id = try editor.panes.add(try editor_view.Main3DView.create(editor.panes.alloc, &font.font, gui.style.config.text_h));
    const main_2d_id = try editor.panes.add(try Ctx2dView.create(editor.panes.alloc, .y));
    const main_2d_id2 = try editor.panes.add(try Ctx2dView.create(editor.panes.alloc, .x));
    const main_2d_id3 = try editor.panes.add(try Ctx2dView.create(editor.panes.alloc, .z));
    const inspector_pane = try editor.panes.add(try panereg.GuiPane.create(editor.panes.alloc, &gui, &inspector_win.vt));
    const texture_pane = try editor.panes.add(try OldGuiPane.create(editor.panes.alloc, editor, .texture, &os9gui));
    const model_pane = try editor.panes.add(try OldGuiPane.create(editor.panes.alloc, editor, .model, &os9gui));
    const model_preview_pane = try editor.panes.add(try OldGuiPane.create(editor.panes.alloc, editor, .model_view, &os9gui));
    editor.edit_state.inspector_pane_id = inspector_pane;

    loadctx.cb("Loading");

    loadctx.cb("Vpk's mounted");

    vpk.timer.log("Vpk dir");

    editor.draw_state.cam3d.fov = config.window.cam_fov;

    if (args.map) |mapname| {
        try editor.loadMap(app_cwd, mapname, &loadctx);
    } else {
        while (!win.should_exit) {
            switch (try pauseLoop(&win, &draw, &launch_win.vt, &gui, gui_dstate, &loadctx, editor, launch_win.should_exit)) {
                .exit => break,
                .unpause => break,
                .cont => continue,
            }
        }
    }

    //TODO with assets loaded dynamically, names might not be correct when saving before all loaded

    loadctx.time = loadctx.gtimer.read();

    graph.c.glEnable(graph.c.GL_CULL_FACE);
    graph.c.glCullFace(graph.c.GL_BACK);

    var ws = Split.Splits.init(alloc);
    defer ws.deinit();
    const main_tab = ws.newArea(.{
        .sub = .{
            .split = .{ .k = .vert, .perc = 0.67 },
            .left = ws.newArea(.{ .pane = main_3d_id }),
            .right = ws.newArea(.{ .pane = inspector_pane }),
        },
    });

    const main_2d_tab = ws.newArea(.{
        .sub = .{
            .split = .{ .k = .vert, .perc = 0.5 },
            .left = ws.newArea(.{ .sub = .{
                .split = .{ .k = .horiz, .perc = 0.5 },
                .left = ws.newArea(.{ .pane = main_3d_id }),
                .right = ws.newArea(.{ .pane = main_2d_id3 }),
            } }),
            .right = ws.newArea(.{ .sub = .{
                .split = .{ .k = .vert, .perc = 0.5 },
                .left = ws.newArea(.{ .sub = .{
                    .split = .{ .k = .horiz, .perc = 0.5 },
                    .left = ws.newArea(.{ .pane = main_2d_id }),
                    .right = ws.newArea(.{ .pane = main_2d_id2 }),
                } }),
                .right = ws.newArea(.{ .pane = inspector_pane }),
            } }),
        },
    });
    try ws.workspaces.append(main_tab);
    try ws.workspaces.append(ws.newArea(.{ .pane = texture_pane }));
    try ws.workspaces.append(ws.newArea(.{ .sub = .{
        .split = .{ .k = .vert, .perc = 0.4 },
        .left = ws.newArea(.{ .pane = model_pane }),
        .right = ws.newArea(.{ .pane = model_preview_pane }),
    } }));
    try ws.workspaces.append(main_2d_tab);

    var tab_outputs = std.ArrayList(struct { graph.Rect, ?usize }).init(alloc);
    defer tab_outputs.deinit();

    var tab_handles = std.ArrayList(Split.ResizeHandle).init(alloc);
    defer tab_handles.deinit();

    var last_frame_group_owner: ?edit.EcsT.Id = null;

    var frame_timer = try std.time.Timer.start();
    var frame_time: u64 = 0;
    win.grabMouse(true);
    main_loop: while (!win.should_exit) {
        if (win.isBindState(config.keys.quit.b, .rising) or pause_win.should_exit)
            break :main_loop;
        if (win.isBindState(config.keys.pause.b, .rising)) {
            editor.paused = !editor.paused;
        }
        if (console_active)
            editor.panes.grab.override();

        if (editor.paused) {
            switch (try pauseLoop(&win, &draw, &pause_win.vt, &gui, gui_dstate, &loadctx, editor, pause_win.should_exit)) {
                .cont => continue :main_loop,
                .exit => break :main_loop,
                .unpause => editor.paused = false,
            }
        }
        draw.real_screen_dimensions = win.screen_dimensions.toF();

        //win.grabMouse(editor.draw_state.grab.is);
        win.grabMouse(editor.panes.grab.was_grabbed);
        win.pumpEvents(.poll);
        //POSONE please and thank you.
        frame_time = frame_timer.read();
        frame_timer.reset();
        const perc_of_60fps: f32 = @as(f32, @floatFromInt(frame_time)) / std.time.ns_per_ms / 16;
        //if (win.mouse.pos.x >= draw.screen_dimensions.x - 40)
        //    graph.c.SDL_WarpMouseInWindow(win.win, 10, win.mouse.pos.y);

        editor.edit_state.mpos = win.mouse.pos;

        const is_full: Gui.InputState = .{ .mouse = win.mouse, .key_state = &win.key_state, .keys = win.keys.slice(), .mod_state = win.mod };
        const is = is_full;
        try os9gui.resetFrame(is, &win);

        const cam_state = graph.ptypes.Camera3D.MoveState{
            .down = win.bindHigh(config.keys.cam_down.b),
            .up = win.bindHigh(config.keys.cam_up.b),
            .left = win.bindHigh(config.keys.cam_strafe_l.b),
            .right = win.bindHigh(config.keys.cam_strafe_r.b),
            .fwd = win.bindHigh(config.keys.cam_forward.b),
            .bwd = win.bindHigh(config.keys.cam_back.b),
            .mouse_delta = if (editor.panes.grab.was_grabbed) win.mouse.delta.scale(editor.config.window.sensitivity_3d) else .{ .x = 0, .y = 0 },
            .scroll_delta = win.mouse.wheel_delta.y,
            .speed_perc = @as(f32, if (win.bindHigh(config.keys.cam_slow.b)) 0.1 else 1) * perc_of_60fps,
        };

        const winrect = graph.Rec(0, 0, draw.screen_dimensions.x, draw.screen_dimensions.y);
        gui.clamp_window = winrect;
        graph.c.glEnable(graph.c.GL_BLEND);
        try editor.update(&win);
        //TODO move this back to POSONE once we can render 3dview to any fb
        //this is here so editor.update can create a thumbnail from backbuffer before its cleared
        try draw.begin(0x3d8891ff, win.screen_dimensions.toF());

        { //Hacks to update gui
            const new_id = editor.selection.getGroupOwnerExclusive(&editor.groups);
            const tool_changed = editor.gui_crap.tool_changed;
            if (new_id != last_frame_group_owner or tool_changed) {
                inspector_win.vt.needs_rebuild = true;
            }
            editor.gui_crap.tool_changed = false;
            last_frame_group_owner = new_id;
        }
        //const tab = tabs[editor.draw_state.tab_index];
        //const areas = Split.fillBuf(tab.split, &areas_buf, winrect);

        try gui.pre_update(gui.windows.items);
        if (win.isBindState(config.keys.toggle_console.b, .rising)) {
            console_active = !console_active;
        }
        ws.doTheSliders(win.mouse.pos, win.mouse.delta, win.mouse.left);
        try ws.setWorkspaceAndArea(editor.draw_state.tab_index, winrect);

        for (ws.getTab()) |out| {
            const pane_area = out[0];
            const pane = out[1] orelse continue;
            if (editor.panes.get(pane)) |pane_vt| {
                //TODO put this in the places that should have it 2
                editor.handleMisc3DKeys(ws.workspaces.items);
                const owns = editor.panes.grab.tryOwn(pane_area, &win, pane);
                editor.panes.grab.current_stack_pane = pane;
                if (owns) {
                    editor.edit_state.lmouse = win.mouse.left;
                    editor.edit_state.rmouse = win.mouse.right;
                } else {
                    editor.edit_state.lmouse = .low;
                    editor.edit_state.rmouse = .low;
                }
                if (pane_vt.draw_fn) |drawf| {
                    drawf(pane_vt, pane_area, editor, .{ .draw = &draw, .win = &win, .camstate = cam_state }, pane);
                }
            }
        }
        if (console_active) {
            console_win.focus(&gui);
            console_win.area.dirty(&gui);
            try gui.update(&.{&console_win.vt});
            try gui.window_collector.append(&console_win.vt);
        }

        editor.panes.grab.endFrame();

        try os9gui.drawGui(&draw);
        const wins = gui.window_collector.items;
        try gui.draw(gui_dstate, false, wins);
        gui.drawFbos(&draw, wins);

        draw.setViewport(null);
        try loadctx.loadedSplash(win.keys.len > 0);
        try draw.end(editor.draw_state.cam3d);
        win.swap();
    }
    if (editor.edit_state.saved_at_delta != editor.undoctx.delta_counter) {
        win.should_exit = false;
        win.pumpEvents(.poll); //Clear quit keys
        editor.paused = true; //Needed for pause loop, hacky
        while (!win.should_exit) {
            if (editor.edit_state.saved_at_delta == editor.undoctx.delta_counter) {
                break; //The map has been saved async
            }
            switch (try pauseLoop(&win, &draw, &nag_win.vt, &gui, gui_dstate, &loadctx, editor, nag_win.should_exit)) {
                .exit => break,
                .unpause => break,
                .cont => continue,
            }
        }
    }

    std.process.cleanExit();
}

pub fn main() !void {
    var gpa = std.heap.GeneralPurposeAllocator(.{
        .stack_trace_frames = if (IS_DEBUG) 0 else 0,
    }){};
    const alloc = gpa.allocator();
    var arg_it = try std.process.argsWithAllocator(alloc);
    defer arg_it.deinit();

    const Arg = graph.ArgGen.Arg;
    const args = try graph.ArgGen.parseArgs(&.{
        Arg("map", .string, "vmf or json to load"),
        Arg("basedir", .string, "base directory of the game, \"Half-Life 2\""),
        Arg("gamedir", .string, "directory of gameinfo.txt, \"Half-Life 2/hl2\""),
        Arg("fgddir", .string, "directory of fgd file"),
        Arg("fgd", .string, "name of fgd file"),
        Arg("nthread", .number, "How many threads."),
        Arg("gui_scale", .number, "Scale the gui"),
        Arg("gui_font_size", .number, "pixel size of font"),
        Arg("gui_item_height", .number, "item height in pixels / gui_scale"),
        Arg("game", .string, "Name of a game defined in config.vdf"),
        Arg("custom_cwd", .string, "override the directory used for game"),
        Arg("fontfile", .string, "load custom font"),
        Arg("display_scale", .number, "override detected display scale, should be ~ 0.2-3"),
        Arg("config", .string, "load custom config, relative to cwd"),
    }, &arg_it);
    try wrappedMain(alloc, args);

    // if the application is quit while items are being loaded in the thread pool, we get spammed with memory leaks.
    // There is no benefit to ensuring those items are free'd on exit.
    if (IS_DEBUG)
        _ = gpa.detectLeaks(); // Not deferred, so on error there isn't spam
}
